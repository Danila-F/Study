.SUBCKT PGB1010603  1   2
*       TERMINALS:  T1  T2
*T. Chenoski  05/31/2004
L1 1 3 0.001u
C1 3 2 0.050p ic=0V
D1 4 3 DZ
D2 3 5 DZ
S1 4 2 1 2 ESDmod OFF
S2 2 5 2 1 ESDmod OFF
.MODEL ESDmod SW(RON=1 ROFF=2G VT=265 VH=235)
.MODEL DZ     D (IS=321F RS=0.1 N=1.5 IBV=10N BV=150 CJO=0.001p TT=1p)
.ENDS

.SUBCKT PGB102ST23  1   2   3
*       TERMINALS:  T1  T2  T3
* Littelfuse 
* T. Chenoski  03/15/2004
*
XpgbA 1 3 PGBdev
XpgbB 2 3 PGBdev
.SUBCKT PGBdev 1 2
L1 1 3 0.001u
C1 3 2 0.050p ic=0V
D1 4 3 DZ
D2 3 5 DZ
S1 4 2 1 2 ESDmod OFF
S2 2 5 2 1 ESDmod OFF
.MODEL ESDmod SW (RON=1 ROFF=2G VT=265 VH=235)
.MODEL DZ     D  (IS=321F RS=0.1 N=1.5 IBV=10N BV=150 CJO=0.001p TT=1p)
.ENDS
.ENDS

.SUBCKT PGB1040805  1   2   3   4   5   6
*       TERMINALS:  T1  T2  T3  T4  T5  T6
* Littelfuse 
* T. Chenoski  06/17/2005
*
XpgbA 1 2 PGBdev
XpgbB 3 2 PGBdev
XpgbC 4 5 PGBdev
XpgbD 6 5 PGBdev
Rshrt 2 5 0.0001
.SUBCKT PGBdev 1 2
L1 1 3 0.001u
C1 3 2 0.050p ic=0V
D1 4 3 DZ
D2 3 5 DZ
S1 4 2 1 2 ESDmod OFF
S2 2 5 2 1 ESDmod OFF
.MODEL ESDmod SW (RON=1 ROFF=2G VT=265 VH=235)
.MODEL DZ     D  (IS=321F RS=0.1 N=1.5 IBV=10N BV=150 CJO=0.001p TT=1p)
.ENDS
.ENDS
;$SpiceType=AMBIGUOUS
