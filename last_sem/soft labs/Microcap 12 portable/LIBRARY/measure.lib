*********************
* .MEASURE LIBRARY  *
*********************
***********************************
*    MEASURES FOR AC ANALYSIS     *
***********************************

* CUTOFF FREQUENCY OF A LOW PASS FILTER EXPRESSION X
* MEASUREMENT TAKEN DB DOWN FROM PEAK
* EXAMPLE: FC_LP(DB(V(OUT)),3.01)
.BEGIN_MEASURE FC_LP(X,DB=3.01)
.MEASURE AC PK MAX X FROM=0 TO=FMAX
.MEASURE AC F2 FIND F WHEN X=PK-DB FALL=FIRST
.MEASURE AC FC_LP PARAM F2
.END_MEASURE FC_LP

* CUTOFF FREQUENCY OF A HIGH PASS FILTER EXPRESSION X
* MEASUREMENT TAKEN DB DOWN FROM PEAK
* EXAMPLE: FC_HP(DB(V(OUT)),3.01)
.BEGIN_MEASURE FC_HP(X,DB=3.01)
.MEASURE AC PK MAX X FROM=0 TO=FMAX
.MEASURE AC F2 FIND F WHEN X=PK-DB RISE=1
.MEASURE AC FC_HP PARAM F2
.END_MEASURE FC_HP

* BANDWIDTH OF BAND PASS OR BAND REJECT FILTER EXPRESSION X
* MEASUREMENT TAKEN DB DOWN FROM PEAK
* EXAMPLE: BW(DB(V(OUT)),3.01)
.BEGIN_MEASURE BW(X,DB=3.01)
.MEASURE AC PK MAX X FROM=FMIN TO=FMAX
.MEASURE AC F1 FIND F WHEN X=PK-DB RISE=FIRST
.MEASURE AC F2 FIND F WHEN X=PK-DB FALL=LAST
.MEASURE AC BW PARAM F2-F1
.END_MEASURE BW

************************************
* MEASURES FOR TRANSIENT ANALYSIS  *
************************************

* PULSE WIDTH OF WAVEFORM EXPRESSION X
* EXAMPLE: PULSE_WIDTH(V(OUT1))
.BEGIN_MEASURE PULSE_WIDTH(X) 
.MEASURE TRAN AVG1 AVG X
.MEASURE TRAN T1 TARG X VAL=AVG1 RISE=1
.MEASURE TRAN T2 TARG X VAL=AVG1 FALL=1
.MEASURE TRAN PULSE_WIDTH PARAM T2-T1
.END_MEASURE PULSE_WIDTH

* OVERSHOOT OF WAVEFORM EXPRESSION X
* EXAMPLE: OVERSHOOT(V(OUT1))
.BEGIN_MEASURE OVERSHOOT(X,TLIMIT)
.MEASURE TRAN AVG1 AVG X
.MEASURE TRAN PK1 MAX X FROM=0 TO=TLIMIT
.MEASURE TRAN T1 TARG X VAL=AVG1 RISE=1
.MEASURE TRAN T2 TARG X VAL=AVG1 FALL=1
.MEASURE TRAN BASE FIND X AT=T1+.8*(T2-T1)
.MEASURE TRAN OVERSHOOT PARAM PK1-BASE
.END_MEASURE OVERSHOOT

* PULSE FREQUENCY OF WAVEFORM EXPRESSION X
* EXAMPLE: PULSE_FREQ(V(OUT1)) 
.BEGIN_MEASURE PULSE_FREQ(X) 
.MEASURE TRAN AVG1 AVG X
.MEASURE TRAN T1 TARG X VAL=AVG1 RISE=1
.MEASURE TRAN T2 TARG X VAL=AVG1 RISE=2
.MEASURE TRAN TP PARAM T2-T1
.MEASURE TRAN PULSE_FREQ PARAM 1/TP
.END_MEASURE PULSE_FREQ

* PULSE PERIOD OF WAVEFORM EXPRESSION X
* EXAMPLE: PULSE_PERIOD(V(OUT1))
.BEGIN_MEASURE PULSE_PERIOD(X) 
.MEASURE TRAN AVG1 AVG X
.MEASURE TRAN T1 TARG X VAL=AVG1 RISE=1
.MEASURE TRAN T2 TARG X VAL=AVG1 RISE=2
.MEASURE TRAN PULSE_PERIOD PARAM T2-T1
.END_MEASURE PULSE_PERIOD

* DELAY FROM WAVEFORM EXPRESSION X TO WAVEFROM EXPRESSION Y
* EXAMPLE: PULSE_DELAY(V(OUT1),V(OUT2))
.BEGIN_MEASURE PULSE_DELAY(X,Y)
.MEASURE TRAN AVG1 AVG X
.MEASURE TRAN AVG2 AVG X
.MEASURE TRAN T1 TARG X VAL=AVG1 RISE=1
.MEASURE TRAN T2 TARG Y VAL=AVG2 RISE=1
.MEASURE TRAN PULSE_DELAY PARAM T2-T1
.END_MEASURE PULSE_DELAY

* POWER FACTOR CALCULATION USING THE INPUT SOURCE SRC
* EXAMPLE: POWER_FACTOR(V1,100M)
.BEGIN_MEASURE POWER_FACTOR(SRC,TSTART=0) 
.MEASURE TRAN PAPPI RMS -I(SRC) FROM=TSTART
.MEASURE TRAN PAPPV RMS V(SRC) FROM=TSTART
.MEASURE TRAN PAPP PARAM=PAPPV*PAPPI
.MEASURE TRAN PREAL AVG V(SRC)*(-I(SRC)) FROM=TSTART
.MEASURE TRAN POWER_FACTOR PARAM=PREAL/PAPP
.END_MEASURE POWER_FACTOR

* LEGACY FUNCTION: PULSE WIDTH OF WAVEFORM EXPRESSION X
* EXAMPLE: PW(V(OUT1))
.BEGIN_MEASURE PW(X) 
.MEASURE TRAN AVG1 AVG X
.MEASURE TRAN T1 TARG X VAL=AVG1 RISE=1
.MEASURE TRAN T2 TARG X VAL=AVG1 FALL=1
.MEASURE TRAN PW PARAM T2-T1
.END_MEASURE PW
********************************************
* FALLTIME OF WAVEFORM EXPRESSION X        *
* BASED ON 90% TO 10% OF PEAK VALUE        *
* Example: FALLTIME(V(OUT)) measures the  * 
* fall time of the waveform V(OUT) vs. T   *
********************************************
.BEGIN_MEASURE FALLTIME(X)
.MEASURE TRAN PEAK1 MAX X FROM=0 TO=TMAX
.MEASURE TRAN T1 FIND T WHEN X=PEAK1*.9 FALL=FIRST
.MEASURE TRAN T2 FIND T WHEN X=PEAK1*.1 FALL=FIRST 
.MEASURE TRAN FALLTIME PARAM T2-T1
.END_MEASURE FALLTIME

********************************************
* RISETIME OF WAVEFORM EXPRESSION X        *
* BASED ON 10% TO 90% OF PEAK VALUE        *
* Example: RISETIME(V(OUT)) measures the  * 
* rise time of the waveform V(OUT) vs. T   *
********************************************
.BEGIN_MEASURE RISETIME(X)
.MEASURE TRAN PEAK1 MAX X FROM=0 TO=TMAX
.MEASURE TRAN T1 FIND T WHEN X=PEAK1*.1 RISE=FIRST
.MEASURE TRAN T2 FIND T WHEN X=PEAK1*.9 RISE=FIRST
.MEASURE TRAN RISETIME PARAM T2-T1
.END_MEASURE RISETIME
;$SpiceType=AMBIGUOUS
